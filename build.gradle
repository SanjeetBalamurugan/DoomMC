plugins {
    id 'fabric-loom' version "${loom_version}"
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    mavenCentral()
    maven { url 'https://maven.fabricmc.net/' }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.minecraft_version}+build.${project.yarn_build}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

/* =======================
   JNI CONFIG (Linux aarch64)
   ======================= */

def jniBuildDir = file("$buildDir/jni")
def nativeCMakeDir = file("native")
def jniLibName = "libdoomjni.so"

tasks.register('configureJNI', Exec) {
    workingDir = nativeCMakeDir
    commandLine "cmake",
            "-S", ".",
            "-B", jniBuildDir.absolutePath,
            "-DCMAKE_BUILD_TYPE=Release",
            "-DCMAKE_TOOLCHAIN_FILE=${nativeCMakeDir}/toolchains/linux-aarch64.cmake"
}

tasks.register('buildJNI', Exec) {
    dependsOn 'configureJNI'
    commandLine "cmake",
            "--build", jniBuildDir.absolutePath,
            "--config", "Release"
}

tasks.register('copyJNILib', Copy) {
    dependsOn 'buildJNI'
    from {
        fileTree(jniBuildDir).matching {
            include "**/${jniLibName}"
        }
    }
    into "$buildDir/resources/main/native/linux-aarch64"
}

processResources {
    dependsOn 'copyJNILib'
    
    inputs.properties([
        mod_id: project.mod_id,
        mod_name: project.mod_name,
        mod_author: project.mod_author,
        mod_version: project.mod_version,
        license: project.license,
        description: project.description,
        java_version: project.java_version,
        minecraft_version: project.minecraft_version,
        loader_version: project.loader_version,
        version: project.mod_version
    ])

    filesMatching("fabric.mod.json") {
        expand(inputs.properties)
    }
}
