plugins {
    id 'fabric-loom' version "${loom_version}"
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    mavenCentral()
    maven { url 'https://maven.fabricmc.net/' }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.minecraft_version}+build.${project.yarn_build}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

def nativeBuildDir = "${buildDir}/native"
def doomLibDir = "${buildDir}/doom_lib"

task buildDoomLib {
    doLast {
        mkdir(doomLibDir)
        exec {
            workingDir 'DOOM'
            commandLine 'cmake', '-S', 'linuxdoom-1.10', '-B', "${doomLibDir}"
        }
        exec {
            workingDir "${doomLibDir}"
            commandLine 'cmake', '--build', '.', '--config', 'Release'
        }
    }
}

task buildJNI {
    dependsOn buildDoomLib
    doLast {
        mkdir(nativeBuildDir)
        exec {
            workingDir 'native'
            commandLine 'cmake', '-S', '.', '-B', "${nativeBuildDir}"
        }
        exec {
            workingDir "${nativeBuildDir}"
            commandLine 'cmake', '--build', '.', '--config', 'Release'
        }
    }
}

task copyJNILib(type: Copy) {
    dependsOn buildJNI
    from("${nativeBuildDir}/lib") {
        include "libdoomjni.*"
    }
    into("${buildDir}/resources/main/native")
    rename { filename ->
        if (filename.endsWith('.dylib')) {
            return 'libdoomjni.dylib'
        } else if (filename.endsWith('.so')) {
            return 'libdoomjni.so'
        } else if (filename.endsWith('.dll')) {
            return 'doomjni.dll'
        }
        return filename
    }
}

processResources {
    dependsOn copyJNILib

    inputs.properties([
        mod_id: project.mod_id,
        mod_name: project.mod_name,
        mod_author: project.mod_author,
        mod_version: project.mod_version,
        license: project.license,
        description: project.description,
        java_version: project.java_version,
        minecraft_version: project.minecraft_version,
        loader_version: project.loader_version,
        version: project.mod_version
    ])

    filesMatching("fabric.mod.json") { expand(inputs.properties) }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = project.java_version.toInteger()
}

java {
    withSourcesJar()
    sourceCompatibility = JavaVersion.toVersion(project.java_version)
    targetCompatibility = JavaVersion.toVersion(project.java_version)
}

jar {
    inputs.property "archivesName", project.base.archivesName
    from("LICENSE") {
        rename { "${it}_${inputs.properties.archivesName}" }
    }
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }
    repositories {
    }
}