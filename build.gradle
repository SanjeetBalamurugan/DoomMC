plugins {
    id 'fabric-loom' version "${loom_version}"
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    mavenCentral()
    maven { url 'maven.fabricmc.net' }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.minecraft_version}+build.${project.yarn_build}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

def jniBuildDir = file("$buildDir/jni")
def nativeCMakeDir = file("native")
def os = org.gradle.internal.os.OperatingSystem.current()
def jniLibName = os.isWindows() ? "doomjni.dll" : os.isMacOsX() ? "libdoomjni.dylib" : "libdoomjni.so"

tasks.register('configureJNI', Exec) {
    workingDir = nativeCMakeDir
    commandLine "cmake", "-S", ".", "-B", jniBuildDir.absolutePath, "-DCMAKE_BUILD_TYPE=Release"
}

tasks.register('buildJNI', Exec) {
    dependsOn 'configureJNI'
    workingDir = nativeCMakeDir
    commandLine "cmake", "--build", jniBuildDir.absolutePath, "--config", "Release"
}

tasks.register('copyJNILib', Copy) {
    dependsOn 'buildJNI'
    from {
        fileTree(jniBuildDir).matching { include "**/${jniLibName}" }
    }
    into "$buildDir/resources/main/native"
}

processResources {
    dependsOn 'copyJNILib'
    inputs.properties([
        mod_id: project.mod_id,
        mod_name: project.mod_name,
        mod_author: project.mod_author,
        mod_version: project.mod_version,
        license: project.license,
        description: project.description,
        java_version: project.java_version,
        minecraft_version: project.minecraft_version,
        loader_version: project.loader_version,
        version: project.mod_version
    ])
    filesMatching("fabric.mod.json") { expand(inputs.properties) }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = project.java_version.toInteger()
}

java {
    withSourcesJar()
    sourceCompatibility = JavaVersion.toVersion(project.java_version)
    targetCompatibility = JavaVersion.toVersion(project.java_version)
}

jar {
    dependsOn 'copyJNILib'
    from("$buildDir/resources/main/native") {
        into("native")
    }
    from(rootProject.file("LICENSE")) {
        rename { String fileName -> "${fileName}_${base.archivesName.get()}" }
    }
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }
}
